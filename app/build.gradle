plugins {
    alias(libs.plugins.android.application)
}

Properties values = new Properties()
values.load(new FileInputStream(project.rootProject.file('local.properties')))

android {
    namespace 'in.atulpatare.ranobem'
    compileSdk 35

    defaultConfig {
        applicationId "in.atulpatare.ranobem"
        minSdk 24
        targetSdk 36
        versionCode 3
        versionName "3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue("string", "ad_app_id", values.getProperty("ad_app_id", "ca-app-pub-3940256099942544~3347511713"))
        resValue("string", "ad_read_manga_video_id", values.getProperty("ad_read_manga_video_id", "ca-app-pub-3940256099942544/5224354917"))
    }

    flavorDimensions "version"
    productFlavors {
        free {
            dimension "version"
            buildConfigField "boolean", "IS_PRO", "false"
        }
        pro {
            dimension "version"
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"

            buildConfigField "boolean", "IS_PRO", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(path: ':core')
    implementation libs.glide
    annotationProcessor libs.compiler
    implementation libs.touchimageview
    implementation(libs.play.services.ads)


    def room_version = "2.7.2"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
}